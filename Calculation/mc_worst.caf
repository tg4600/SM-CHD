
{ This definition is required at the top of all calculations }

GLOBAL ROUTINE calculation ( current_sample,
                 current_test_number,
                 current_component_name )

    JOIN STANDARD_LIBRARY STD_ARRAY
    JOIN LIBRARY $LIB_UTILS               
    JOIN STANDARD_LIBRARY STD_DATABASE

    DECLARE TxtArray, p, p0, o, res, num, i
    ARRAY TxtArray   

    a_id  = SELECT test.analysis WHERE test_number = current_test_number
    a_ver = SELECT test.analysis_version
    
    o = SELECT versioned_component.order_number 
        WHERE analysis = a_id
          AND analysis_version = a_ver
          AND name = current_component_name

    p = SELECT versioned_component.order_number 
        WHERE analysis = a_id
          AND analysis_version = a_ver
          AND calculation = "MC_WORST"

    IF p = o THEN
        p = 0
    ELSE
        p0 = SELECT versioned_component.order_number
        WHILE (p0 < o) AND (p0 <> EMPTY)
            p = p0
        NEXT versioned_component
            p0 = SELECT versioned_component.order_number
        ENDWHILE
    ENDIF

    o   = SELECT result.order_number 
          WHERE  test_number = current_test_number
            AND  calculation = "MEAN_CONT"
            AND  order_number > p
            AND  order_number < o
            {AND  Text <> "NEW_TEST"}

    IF o   = EMPTY THEN
        fm("No MEAN_CONT, with valid result, on current test.")
        RETURN (EMPTY)
    ENDIF

    WHILE o   <> EMPTY DO
        TxtArray[size_of_array(TxtArray) + 1, 1] = o
    NEXT result
        o   = SELECT result.order_number 
    ENDWHILE

    i = 1

    WHILE i <= size_of_array(TxtArray) DO
        TxtArray[i, 2] = SELECT result.text WHERE test_number = current_test_number and order_number = TxtArray[i, 1]
        IF (LEFTSTRING(TxtArray[i, 2], 1) = "<") THEN
            TxtArray[i, 3] = -(SELECT result.value)
        ELSE
            TxtArray[i, 3] = SELECT result.value
        ENDIF
        i = i + 1
    ENDWHILE

    i = 1
    o   = TxtArray[i, 1]
    res = TxtArray[i, 2]
    num = NUMERIC(TxtArray[i, 3])

    WHILE i < size_of_array(TxtArray) DO
        i = i + 1
        IF (num < TxtArray[i, 3]) THEN
            o   = TxtArray[i, 1]
            res = TxtArray[i, 2]
            num = TxtArray[i, 3]
        ENDIF
    ENDWHILE

    Return(Res)

ENDROUTINE
