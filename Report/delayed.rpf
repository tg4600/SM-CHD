{ Title  : LPEDELAYED
{ Author : Lene Pedersen Chr. Hansens Laboratorium       }
{ Date   : 12-02-1993                                    }
{ Purpose: ALL DELAYED SAMPLE SUMMARY (Date expected)    }

JOIN LIBRARY $INSTALL_WINDOW

SET FORMAT "9999999999"
SET HEADER "                                                            Page ##"

ENABLE WINDOWS
SET PRINTERCODES TRUE

ARRAY sampid
ARRAY jobid
ARRAY expdate
ARRAY reqdate

curdate = today

count= 0

s_count = SELECT COUNT sample
                WHERE (  (sample.dateresexp <> "") AND
                        (sample.dateresreq <> "") AND
                        (status <> "C") AND
                        (status <> "A") AND
                        (status <> "X") AND
                        (on_wks = TRUE) )


sampno = SELECT sample.id_numeric
                 WHERE ( (dateresexp <> "") AND
                        (dateresreq <> "") AND
                        (status <> "C") AND
                        (status <> "A") AND
                        (status <> "X") AND
                        (on_wks = TRUE)  )
                      ORDER ON id_numeric

IF sampno <> empty THEN

   set_up_install_window_class ()

   CREATE OBJECT INSTALL_WINDOW_CLASS, percent_bar

   percent_bar . header = (GET_USER_MESSAGE ("ACTIVE_PERCENT_BAR_NAME" ,1))
   percent_bar . footer = " "
   percent_bar . row = 10
   percent_bar . start_value = 0
   percent_bar . end_value = s_count
   percent_bar . start_install ()


   WHILE sampno <> EMPTY DO
      IF (SELECT sample.dateresexp) > (SELECT sample.dateresreq) THEN
         count= count+ 1
         reqdate [count] = DATE(SELECT sample.dateresreq)
         expdate [count] = DATE(SELECT sample.dateresexp)
         sampid [count] = sampno
         jobid [count] = SELECT sample.job_name
      ENDIF
      NEXT sample
      sampno = SELECT sample.id_numeric
      percent_bar . increase_one ()

   ENDWHILE

   percent_bar . end_install ()
   number_of_samp = count

LITERAL

         !U Sample Tracking Options - List delayed samples per $curdate____!U


$ENDLITERAL

LITERAL
      _____________________________________________________________________
     | Sample ID | Job ID                |  Date Required |  Date Expected |
     |___________|_______________________|________________|________________|
$ENDLITERAL

ON NEW PAGE
LITERAL                                                   |
      _____________________________________________________________________
     | Sample ID |  Job ID               |  Date Required |  Date Expected |
     |___________|_______________________|________________|________________|
$ENDLITERAL
ENDON
   count = 1
   WHILE count <= number_of_samp DO
      pr_sampid = RIGHTSTRING(SUBSTRING(sampid [count],1,10),10)
      pr_jobid = jobid [count]
      pr_reqdate   = reqdate [count]
      pr_expdate = expdate [count]
LITERAL
     |$pr_sampid | $pr_jobid____________ |  $pr_reqdate_  |  $pr_expdate_  |
     |___________|_______________________|________________|________________|
$ENDLITERAL
      count = count + 1
   ENDWHILE

ELSE
   DISPLAY "There are currently no delayed Samples"
         ON LINE 2 FROM 2 IN WINDOW disp_win
   DISPLAY "Press any key to continue . . . ."
         ON LINE 3 FROM 2 IN WINDOW disp_win
   dummy = getkey
ENDIF

