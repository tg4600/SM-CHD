{Created by HBe on January 3, 2002. Issue 1380. Enables users to increase the usage count of 
 standard versions by 1. This is to overcome an internal SampleManager error. Occationally the usage
 count for standards is not increased when a standard is used in a worksheet. This created problems with
 the unique text_id for the sample table.}


SET name "DEFER/"
ENABLE WINDOWS
SET COMPILE_OPTION DECLARE
JOIN LIBRARY toolbox
JOIN LIBRARY $LIB_UTILS


DECLARE std_id					{Standard identity}
DECLARE std_ver					{Standard version}
DECLARE std_count                               {Usage count on standard_versions}
DECLARE new_count                               {Increased Usage count on standard_versions}
DECLARE l_k_p					{last key pressed}
DECLARE rc					{return_code}
DECLARE empty_arr				{An empty select array}

std_id = EMPTY
l_k_p  = EMPTY
ARRAY empty_arr



   SET DISPLAY FALSE
   rc = prompt_user ( "standard", "Increase Standard Usage Count by One",
                     EMPTY,
                     TRUE, "Please Select standard Identity: ",
                     empty_arr, std_id, l_k_p )

   IF (rc) AND (NOT BLANK (std_id)) THEN
      handle_std_ver ( std_id )
   ELSE
      FLASH_MESSAGE("Problem selecting standard!", TRUE)
      EXIT
   ENDIF


{---------------------------------------------------------------------------------------------------------}
ROUTINE handle_std_ver (std_id)

std_ver = SELECT MAX standard_versions.version
          WHERE identity = std_id

IF std_ver = EMPTY THEN
      FLASH_MESSAGE("Problem selecting standard version!", TRUE)
      EXIT
ENDIF


START READ TRANSACTION "find_use"

std_count = SELECT standard_versions.usage_count FOR UPDATE
            WHERE identity = std_id
            AND   version = std_ver

IF std_count = EMPTY THEN
      FLASH_MESSAGE("Problem finding current usage count!", TRUE)
      EXIT
ENDIF


IF NOT (std_count > -1) THEN
      FLASH_MESSAGE("The current usage count is negative!", TRUE)
      EXIT
ENDIF


new_count = std_count + 1

ASSIGN standard_versions.usage_count = new_count
START WRITE TRANSACTION "save_use"
UPDATE standard_versions
COMMIT

flash_message("Updated usage count for standard ": STRIP(std_id) : " version " : STRIP(std_ver) :
              " . The new count is: " : STRIP(new_count):".", TRUE)



ENDROUTINE

