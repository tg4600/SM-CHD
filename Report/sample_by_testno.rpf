{**AS1661 HBe 18-11-05 New feature to identify sample fro. a given test number }

SET NAME "DEFER/"
ENABLE WINDOWS
SET COMPILE_OPTION DECLARE

JOIN STANDARD_LIBRARY STD_UTILS

JOIN LIBRARY $LIB_UTILS
JOIN LIBRARY TOOLBOX



DECLARE test, test_no, sample_id, analysis, test_count, t_status, a_version, id_text, s_status, s_product, s_prod_ver

test       = EMPTY

WHILE get_identity (  test , "Enter a Test Number" ,
                              "Sample by Test Number" , "TEST" , empty ) DO
                              
handle_test (test)

ENDWHILE



ROUTINE handle_test (test_no)

JOIN LIBRARY $LIB_UTILS
JOIN STANDARD_LIBRARY STD_PROMPT



sample_id  = SELECT test . sample
             WHERE   test_number = test_no

analysis   = SELECT test . analysis
test_count = SELECT test . test_count
t_status   = SELECT test . status
a_version  = SELECT test  . analysis_version

id_text    = SELECT sample . id_text
             WHERE id_numeric = sample_id

s_status   = SELECT sample . status
s_product  = SELECT sample . product
s_prod_ver = SELECT sample . product_version 

IF s_product   = EMPTY THEN s_product = "N.A."
ENDIF
IF s_prod_ver = EMPTY THEN s_prod_ver = "N.A."
ENDIF


IF ((sample_id  = EMPTY) OR
    (analysis   = EMPTY) OR
    (test_count = EMPTY) OR
     (t_status   = EMPTY) OR
     (id_text    = EMPTY) OR
     (s_status   = EMPTY) OR
     (s_product  = EMPTY) OR
     (s_prod_ver = EMPTY)
    ) THEN
            FLASH_MESSAGE("Error obtaining information about this test, please contact your super user", TRUE)    
            RETURN(EMPTY)
ENDIF

DECLARE the_form


CREATE OBJECT "STD_FORM", the_form

{setup defaults}
the_form . header	=  "Displaying information about Test number ":STRIP(test_no):"."
the_form . footer = "Press DO to exit"
the_form . row	=  1
the_form . column	=   2
the_form . width	=  60 
the_form . height	=  12

the_form . add_display ( "ID Numeric: "       : STRIP(sample_id) : ".",                      10,3, prompt_rendition_bold )
the_form . add_display ( "ID Text: "          : STRIP(id_text)   : ".",                      10,4, prompt_rendition_bold )
the_form . add_display ( "Sample Status: "    : STRIP(s_status),                             10,5, prompt_rendition_bold )
the_form . add_display ( "Product/Version: "  : STRIP(s_product):"/":STRIP(s_prod_ver),      10,6, prompt_rendition_bold )

the_form . add_display ( "Analysis/Count: "   : STRIP(analysis)   : "/" : STRIP(test_count), 10,8, prompt_rendition_bold )
the_form . add_display ( "Analysis Version: " : STRIP(a_version)                           , 10,9, prompt_rendition_bold )
the_form . add_display ( "Test Status: "      : STRIP(t_status),                             10,10, prompt_rendition_bold )

the_form . return_behaviour = FORM_RETURN_STAY



the_form . start_prompt ()
the_form . wait_prompt ()
the_form . end_prompt ()

ENDROUTINE

