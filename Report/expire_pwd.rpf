{**1762 HBe 19-12-2006: New function to expire any users password without going into the security functino in SampleManager}

SET NAME "DISPLAY/"

ENABLE WINDOWS

SET COMPILE_OPTION DECLARE
SET NOTPROTECTED


GLOBAL ROUTINE expire_password (VALUE nothing)

   JOIN STANDARD_LIBRARY STD_ARRAY_SELECT
   JOIN LIBRARY $lib_utils
   JOIN LIBRARY toolbox


   DECLARE select_arr, user_id, lkp, old_date
   user_id = EMPTY
   lkp     = EMPTY

   ARRAY select_arr

   select_arr[1,1] = ARRAY_SELECT_EQ
   select_arr[1,2] = "REMOVEFLAG"
   select_arr[1,3] = FALSE

   IF prompt_user ( "PERSONNEL", "Choose User ID for Password Expiry", "IDENTITY", TRUE,
                                 "User ID: ",
                                 select_arr, user_id, lkp )
   THEN
      IF ((user_id <> EMPTY) AND NOT BLANK(user_id)) THEN

         START READ TRANSACTION "expiry date" 

         old_date = SELECT password.expires FOR UPDATE
                    WHERE identity = user_id
         IF ((old_date <> EMPTY) AND NOT BLANK(old_date)) THEN
            START WRITE TRANSACTION "expiry date"
            ASSIGN password.expires   = NOW
            ASSIGN password.frozen    = FALSE
            ASSIGN password.cryptpass = "6t:y>hyoJ-"
            UPDATE password 
            COMMIT
            FLASH_MESSAGE("Password for user ": STRIP(user_id) : " previously set to ": SUBSTRING(old_date,1,11) : " has been expired", TRUE)
         ELSE
            FLASH_MESSAGE("No Password has been assigned to user " :  STRIP(user_id), TRUE)
         ENDIF
      ELSE
         FLASH_MESSAGE("No Identity supplied, No change Made to Password Expiry", TRUE)
      ENDIF
   ELSE
      FLASH_MESSAGE("Cancel chosen, No change Made to Password Expiry", TRUE)
   ENDIF

ENDROUTINE
