SET NAME "/DEFER"
ENABLE WINDOWS

JOIN STANDARD_LIBRARY STD_PROMPT
JOIN STANDARD_LIBRARY STD_UTILS
JOIN STANDARD_LIBRARY STD_BLOCK

JOIN LIBRARY $LIB_UTILS
JOIN LIBRARY TOOLBOX

ARRAY conc_arr
ARRAY new_concs

SET NOTPROTECTED

IF   NOT load_phrase("SMP_CONC", conc_arr) THEN 
    return ( FALSE )
ENDIF

concs = size_of_array(conc_arr)

n = 0
WHILE n<concs DO
   n = n+1
   new_concs[n,1] = conc_arr[n,1]
   new_concs[n,2] = conc_arr[n,2]
ENDWHILE

concs = concs + 1

new_concs[concs,1] = " "
new_concs[concs,2] = "All interest groups"

conc_arr[concs,1] = " "
conc_arr[concs,2] = "All interest groups"


smp_conc = EMPTY

WHILE prompt_user( EMPTY, "Interest area to search samples for",
                   EMPTY, TRUE, "Interest Area:", conc_arr, smp_conc, l_key )
DO
   handle_samples ( smp_conc )
ENDWHILE


ROUTINE  handle_samples ( VALUE smp_conc )

   ARRAY smp_sel_arr

   IF (NOT BLANK(strip(smp_conc))) AND (smp_conc <> EMPTY) THEN
      smp_sel_arr[1,1] = ARRAY_SELECT_EQ
      smp_sel_arr[1,2] = "CONCERN_FUNCTION"
      smp_sel_arr[1,3] = smp_conc
{**AS1438 HBe 10-12-2002}
      smp_sel_arr[2,1] = ARRAY_SELECT_NE
      smp_sel_arr[2,2] = "STATUS"
      smp_sel_arr[2,3] = "A"
{**AE1438}

   ENDIF

   new_conc = EMPTY

   WHILE prompt_user( EMPTY, "Interest area to change samples to", EMPTY,
                      TRUE, "Interest Area:", new_concs, new_conc, l_key )
   DO

      smp_id = EMPTY

      WHILE prompt_user ("SAMPLE","Enter Sample Id to Change Interest On",
                         EMPTY, TRUE, "Sample Id:", smp_sel_arr, smp_id, l_key )
      DO
         the_id = select sample . id_numeric FOR UPDATE
                  where  id_numeric = smp_id
         IF the_id <> LOCKED THEN
            START READ  TRANSACTION "Update Concern"
            ASSIGN sample . concern_function = new_conc
            START WRITE TRANSACTION "Update Concern"
            UPDATE sample
            COMMIT
            flash_message("Sample has been updated",TRUE)
         ENDIF
      ENDWHILE

   ENDWHILE

ENDROUTINE
